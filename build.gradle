import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'org.springframework.boot' version '3.0.3' apply false
    id 'org.jetbrains.kotlin.jvm' version '1.7.22' apply false
    id 'org.jetbrains.kotlin.plugin.spring' version '1.7.22' apply false
    id 'com.vaadin' version '24.0.0' apply false
    id 'io.spring.dependency-management' version '1.1.0'
}

subprojects {
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'org.jetbrains.kotlin.jvm'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
            mavenBom "com.vaadin:vaadin-bom:${vaadinVersion}"
        }
    }


    bootBuildImage {
        imageName = "aymenbenadra/$project.rootProject.name-$project.name"
    }

    bootJar {
        layered {
            application {
                intoLayer("spring-boot-loader") {
                    include "org/springframework/boot/loader/**"
                }
                intoLayer("application")
            }
            dependencies {
                intoLayer("dependencies")
            }
            layerOrder = ["dependencies", "spring-boot-loader", "application"]
        }
    }

    tasks.withType(KotlinCompile).configureEach {
        kotlinOptions {
            freeCompilerArgs = ['-Xjsr305=strict']
            jvmTarget = sourceCompatibility
        }
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}